# Personal zsh customizations

# Find out where this script is located

HERE=${0:a:h:h}

# Avoid duplicates

typeset -U PATH path manpath

# Set platform dependent variables

case $OSTYPE in
    darwin*)

	    LDLIB=DYLD_LIBRARY_PATH
	    export TIME="/usr/bin/time -l"
        
        # Homebrew - Need to use real path since $HOMEBREW_PREFIX isn't set yet
        [[ -f /opt/homebrew/bin/brew ]] && eval $(/opt/homebrew/bin/brew shellenv)
        # Disable automatic cleanup
        export HOMEBREW_NO_INSTALL_CLEANUP=1

        # Use the gnu utilities

        if [ -d $HOMEBREW_PREFIX/opt/coreutils/libexec ]; then
            path=($HOMEBREW_PREFIX/opt/coreutils/libexec/gnubin $path)
            manpath=($HOMEBREW_PREFIX/opt/coreutils/libexec/gnuman $manpath)
        fi 

        # MacTeX
        [ -d /Library/TeX/texbin ] && path=(/Library/TeX/texbin $path)

        ;;

    linux*)
        
	    LDLIB=LD_LIBRARY_PATH
	    export TIME="/usr/bin/time --verbose"

        # Set the display for wsl
        [[ $(uname -a) == *WSL* ]] && export DISPLAY=$(route -n | grep UG | head -n1 | awk '{print $2}'):0
        
        ;;

esac

# My directories

[ -d ~/.local/bin ] && path=(~/.local/bin $path)
[ -d ~/.local/share/man ] && manpath=(~/.local/share/man $manpath)

# Colors

if [[ "$TERM" != "dumb" ]]; then

    eval $(dircolors ${HERE}/zsh/dircolors)

    [ $(command -v lesspipe.sh) ] && export LESSOPEN="|$(which lesspipe.sh) %s"
    [ $(command -v lesspipe) ] && export LESSOPEN="|$(which lesspipe) %s"
    [ $(command -v highlight) ] && 	export LESSOPEN="| $(which highlight) %s --out-format xterm256 -l --force -s solarized-light --no-trailing-nl"

    export LESS=' -R '

fi

# Rust
[ -d ~/.cargo/bin ] && path=(~/.cargo/bin $path)

# Direnv
[ $(command -v direnv) ] && eval "$(direnv hook zsh)"

# Python
PVE=~/.local/PVE

pushd $PVE &> /dev/null

for d in *(N) 
    alias ${d%/}="source $PVE/${d%/}/bin/activate"

popd &> /dev/null

[ -d $HERE/python ] && export PYTHONPATH=$HERE/python

# Since all we're using pyenv for is getting python distributions 
# we don't actually install the hooks

[ -d ~/.pyenv ] && export PYENV_ROOT=~/.pyenv
[ -d ~/.pyenv/bin ] && export path=(~/.pyenv/bin $path)

# Default editor
[ $(command -v nano) ] && {
    export EDITOR=$(which nano)
    export VISUAL=$(which nano)
}

# Bat pager theme

export BAT_THEME="GitHub"

# Set emacs editing mode
bindkey -e
